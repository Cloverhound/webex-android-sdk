import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'checkstyle'

def gitSha() {
    return 'git rev-parse --short HEAD'.execute([], file(".")).text.trim()
}

def getNdkBuild() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return new File(ANDROID_NDK_HOME, 'ndk-build.cmd')
    } else {
        return new File(ANDROID_NDK_HOME, 'ndk-build')
    }
}

task ndkCheck {
    def error = 'set ANDROID_NDK_HOME in your ~/.gradle/gradle.properties'
    assert ANDROID_NDK_HOME != null: error
    assert getNdkBuild().exists(): error
}

task buildNative(type: Exec, dependsOn: 'ndkCheck') {
    def ndkBuildingDir = new File("jni");
    commandLine getNdkBuild(), "--directory", ndkBuildingDir, "NDK_LIBS_OUT=../src/main/jniLibs"
}

task removeCShared (type: Delete, dependsOn: 'buildNative'){
    delete "./src/main/jniLibs/armeabi-v7a/libc++_shared.so"
}

tasks.withType(JavaCompile) { compileTask -> compileTask.dependsOn removeCShared }


task cleanNative(type: Exec, dependsOn: 'ndkCheck') {
    def ndkBuildingDir = new File("jni");

    commandLine getNdkBuild(), "--directory", ndkBuildingDir, "NDK_LIBS_OUT=../src/main/jniLibs", "clean"
}

clean.dependsOn(cleanNative)

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "REGION_API_URL", '"' + "https://ds.ciscospark.com" + '"'
        buildConfigField "String", "WX2_SERVICE_API_URL", '"' + (System.env.LOCUS_BASE_URL ?: "https://wdm-a.wbx2.com/wdm/api/v1") + '"'
        buildConfigField "String", "USERS_API_URL", '"' + (System.env.CONVERSATION_BASE_URL ?: "https://conv-a.wbx2.com/conversation/api/v1/") + '"'
        buildConfigField "String", "ATLAS_API_URL", '"' + "https://atlas-a.wbx2.com/admin/api/v1/" + '"'
        buildConfigField "String", "CALLIOPE_REGISTRAR_URL", '"' + "https://calliope-a.wbx2.com/calliope/api/registrar/v1" + '"'
        buildConfigField "String", "GIT_COMMIT_SHA", '"' + gitSha() + '"'
        buildConfigField "String", "WB_URL", '"https://whiteboard.cisco.com/integrated/index.html"'
        buildConfigField "String", "METRICS_API_URL", '"https://metrics-a.ciscospark.com"'
        buildConfigField "String", "WB_ACL_URL", '"' + "https://acl-a.wbx2.com/acl/api/v1/" + '"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    /*
    productFlavors {
        prod {
            // Use the defaults
        }


        lollipop {
            minSdkVersion 21
        }
    }
    */

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"

        //lm
        dexInProcess false
    }

    testOptions {

        unitTests {
            all {
                maxHeapSize = "2048m"
                maxParallelForks 1

                forkEvery 150
                ignoreFailures false

                testLogging {
                    if (project.hasProperty("DEBUG")) {
                        events "passed", "skipped", "failed", "standardOut", "standardError"
                        showStandardStreams = true
                    } else {
                        events "passed", "skipped", "failed"
                        showStandardStreams = false
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'

    compile libs.supportv13
    compile libs.supportannotation
    compile libs.appcompatv7
    compile libs.recyclerviewv7
    compile libs.cardviewv7
    compile libs.supportdesign
    compile libs.gridlayout

    compile libs.gson

    compile libs.dagger
    provided libs.daggercompiler

    compile libs.retrofit
    compile libs.retrofit_gsonconverter
    compile libs.retrofit_deprecated
    compile libs.okhttp3_for_retrofit1
    compile libs.retrofit_rxjava

    compile libs.okhttp
    compile libs.okhttp_logging
    compile libs.websocket

    compile libs.eventbus

    compile libs.spongycastle_prov
    compile libs.spongycastle_core
    compile libs.kms

    compile libs.ln

    compile libs.wmeaar

    compile libs.rxjava
    compile libs.rxandroid

    compile libs.will

    debugCompile libs.leak_canary
    releaseCompile libs.leak_canary_disabled
    testCompile libs.leak_canary_disabled
    androidTestCompile libs.leak_canary_disabled

    compile libs.rxrelay

    testCompile libs.junit
    testCompile libs.robolectric
    testCompile libs.robolectric_multidex
    testCompile libs.mockito
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.android.support:multidex:1.0.1'
}

task styleCheck(type: Checkstyle) {
    configFile new File(rootDir, "checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/ConversationContract.java'

    classpath = files()
}
